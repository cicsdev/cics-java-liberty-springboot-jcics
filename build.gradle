plugins
{
    id 'org.springframework.boot' version '2.3.0.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'eclipse'
	id 'idea'
	id 'war'
    id 'maven-publish'
}

group = 'com.ibm.cicsdev.springboot'
archivesBaseName='cics-java-liberty-springboot-jcics'
version = '0.1.0'
sourceCompatibility = '1.8'


// If in Eclipse, add Javadoc to the local project classpath
eclipse
{
    classpath
    {
        downloadJavadoc = true
    }
}


repositories
{
	mavenCentral()
}


dependencies 
{
    // Spring Boot web starter
    implementation("org.springframework.boot:spring-boot-starter-web:2.7.1")	

    // Don't include Tomcat in the runtime build, but do put it in WEB-INF so it can be run standalone as well as embedded
    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat:2.7.1")

    // CICS TS V5.5 Maven BOM (as of 19th May 2020)
    compileOnly enforcedPlatform("com.ibm.cics:com.ibm.cics.ts.bom:5.5-20200519131930-PH25409")

    // Don't include JCICS in the final build (no need for version because we have BOM)
    compileOnly("com.ibm.cics:com.ibm.cics.server") 
}


publishing {
    publications {
    	// Publication for JCICS
        maven(MavenPublication) {
            groupId "${group}"
            version "${version}"
            artifactId "${archivesBaseName}" 
            artifact bootWar
        }
    }
    
    // Configure the Maven repository to publish to somewhere which is configurable
    // with environment variables from outside gradle.
    //
    // For example:
    //   gradle build publish \
    //   -Ppublish_repo_releases_url="file://my-folder" \
    //   -Ppublish_repo_releases_name="my-maven-repo"
    //
    repositories {
        maven {
            url = "${publish_repo_releases_url}/${publish_repo_releases_name}"
        }
    }

}